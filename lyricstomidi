#!/usr/bin/env raku

grammar Lyrics {
    token TOP {
        <cword>*
    }
    token cword {
        <word> [ \s+ '--' ]?
    }
    token word {
        | <quotedstring>
        | (\S+)
        | (\s+)
    }
    token quotedstring {
        '"' <qspart>+ '"'
    }
    token qspart {
        | <-[ " \\ ]>+
        | '\\' .
    }
}

for lines() -> $line is copy {
    my $first = '\\n';
    if $line ~~ /^\s*\\/ {
      say $line;
      next;
    }
    my $suffix = '';
    $line ~~ s/(\s*\%.*)?$//;
    $suffix = ~$0 if $0;
    my $/ = Lyrics.parse($line);
    for $/<cword> -> $word is copy {
        $word ~~ s/\s* '--'/-/;
        given $word {
            when .starts-with('\\') {
                print $_;
            }
            when .starts-with('"') {
                s/^\"/"$first/ if $first;
                s/\-\"$/"/ or s/\"$/ "/;
                print $_;
                $first = '';
            }
            when .ends-with('-') {
                if $first {
                  s/\-$//;
                  print "\"\\n{$_}\"";
                  $first = '';
                } else {
                  s/\-$//;
                  .print;
                }
            }
            when '--' {
            }
            when '__' {
            }
            when '_' {
                print '""';
            }
            when /^\s+$/ {
                print $_;
            }
            default {
                print "\"{$first}$_ \"";
                $first = '';
            }
        }
    }
    say $suffix;
}

